import groovy.json.JsonSlurper
import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id "de.undercouch.download" version "4.1.1"
    id "net.researchgate.release" version "2.8.0"
    id "maven-publish"
}

def jdkModules = ""

configurations {
    zip
}

def ballerinaJreLinuxZip = file("$buildDir/distributions/ballerina-jre-linux-${project.version}.zip")
def ballerinaJreWindowsZip = file("$buildDir/distributions/ballerina-jre-windows-${project.version}.zip")
def ballerinaJreMacOSZip = file("$buildDir/distributions/ballerina-jre-macos-${project.version}.zip")

task getModuleList {
    // Reads default modules included in AdoptOpenJre 11
    def defaultModuleFile = new File("$projectDir/resources/adoptOpenJre11_modules.json")
    def defaultModules = new JsonSlurper().parseText(defaultModuleFile.text).modules.join(',')

    // Reads additional modules required for ballerina runtime.
    def additionalModuleFile = new File("$projectDir/resources/additional_modules.json")
    def additionalModules = new JsonSlurper().parseText(additionalModuleFile.text).modules.join(',')
    jdkModules = "${defaultModules},${additionalModules}"
}

task downloadJdkforLinux(type: Download) {
    src project.linuxJdkUrl
    dest new File("$buildDir/temp/downloads/", "jdk-linux.tar.gz")
    overwrite false
    onlyIfModified true
}

task downloadJdkforWindows(type: Download) {
    src project.windowsJdkUrl
    dest new File("$buildDir/temp/downloads/", "jdk-windows.zip")
    overwrite false
    onlyIfModified true
}

task downloadJdkforMac(type: Download) {
    src project.macJdkUrl
    dest new File("$buildDir/temp/downloads/", "jdk-macOS.tar.gz")
    overwrite false
    onlyIfModified true
}

task extractJdkForLinux(type: Copy, dependsOn: downloadJdkforLinux) {
    from tarTree(downloadJdkforLinux.dest)
    into "$buildDir/temp/extracted/jdk-linux"
    includeEmptyDirs = false
}

task extractJdkForWindows(type: Copy, dependsOn: downloadJdkforWindows) {
    from zipTree(downloadJdkforWindows.dest)
    into "$buildDir/temp/extracted/jdk-windows"
    includeEmptyDirs = false
}

task extractJdkForMac(type: Copy, dependsOn: downloadJdkforMac) {
    from tarTree(downloadJdkforMac.dest)
    into "$buildDir/temp/extracted/jdk-mac"
    includeEmptyDirs = false
}

task assembleJreForLinux(type: Exec, dependsOn: extractJdkForLinux) {
    workingDir "$buildDir/temp/extracted/jdk-linux/jdk-${project.jdkVersion}/bin"
    commandLine 'jlink', '--add-modules', jdkModules, '--output', '../../../../generated/ballerina-jre-linux'
}

task assembleJreForWindows(type: Exec, dependsOn: extractJdkForWindows) {
    workingDir "$buildDir/temp/extracted/jdk11-windows/jdk-${project.jdkVersion}/bin"
    commandLine 'jlink.exe', '/c', '--add-modules', jdkModules, '--output', '../../../../generated/ballerina-jre-windows'
}

task assembleJreForMac(type: Exec, dependsOn: extractJdkForMac) {
    workingDir "$buildDir/temp/extracted/jdk11-macOS/jdk-${project.jdkVersion}/bin"
    commandLine 'jlink', '--add-modules', jdkModules, '--output', '../../../../generated/ballerina-jre-mac'
}

task packageJreForLinux(type: Zip, dependsOn: assembleJreForLinux) {
    from fileTree("$buildDir/temp/extracted/jre/ballerina-jre-linux/")
    archiveName "ballerina-jre-linux-${project.version}.zip"
    destinationDirectory = file("$buildDir/distributions/")
    outputs.file ballerinaJreLinuxZip
}

task packageJreForWindows(type: Zip, dependsOn: assembleJreForWindows) {
    from fileTree("$buildDir/temp/extracted/jre/ballerina-jre-windows/")
    archiveName "ballerina-jre-windows-${project.version}.zip"
    destinationDirectory = file("$buildDir/distributions/")
    outputs.file ballerinaJreWindowsZip
}

task packageJreForMac(type: Zip, dependsOn: assembleJreForMac) {
    from fileTree("$buildDir/temp/extracted/jre/ballerina-jre-macOS/")
    archiveName "ballerina-jre-macOS-${project.version}.zip"
    destinationDirectory = file("$buildDir/distributions/")
    outputs.file ballerinaJreMacOSZip
}

def moduleVersion = project.version
if (moduleVersion.indexOf('-') != -1) {
    moduleVersion = moduleVersion.substring(0, moduleVersion.indexOf('-'))
}

task build {
    if (Os.isFamily(Os.FAMILY_UNIX)) {
        dependsOn packageJreForLinux
    } else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        dependsOn packageJreForWindows
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        dependsOn packageJreForMac
    }
}

artifacts {
    zip file: ballerinaJreLinuxZip, builtBy: packageJreForLinux
    zip file: ballerinaJreWindowsZip, builtBy: packageJreForWindows
    zip file: ballerinaJreMacOSZip, builtBy: packageJreForMac
}

release {
    failOnPublishNeeded = false
    failOnUpdateNeeded = false
    failOnUnversionedFiles = false
    failOnCommitNeeded = false
    buildTasks = ['build']
    versionPropertyFile = 'gradle.properties'
    tagTemplate = 'v$version'
    newVersionCommitMessage = "Update to next development version "
    preTagCommitMessage = "Prepare release version "
    tagCommitMessage = "Tag release "

    git {
        requireBranch = "release-${moduleVersion}"
        pushToRemote = 'origin'
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/NipunaRanasinghe/ballerina-jre-test")
            credentials {
                username = System.getenv("packageUser")
                password = System.getenv("packagePAT")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            if (Os.isFamily(Os.FAMILY_UNIX)) {
                artifact source: packageJreForLinux, extension: 'zip'
            } else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                artifact source: packageJreForWindows, extension: 'zip'
            } else if (Os.isFamily(Os.FAMILY_MAC)) {
                artifact source: packageJreForMac, extension: 'zip'
            }
        }
    }
}

task deleteGenertedFiles(type: Delete) {
    delete "$buildDir/temp/generated/"
}

task deleteTemporaryFiles(type: Delete) {
    delete "$buildDir/temp/"
}

assembleJreForLinux.dependsOn getModuleList
assembleJreForWindows.dependsOn getModuleList
assembleJreForMac.dependsOn getModuleList

assembleJreForLinux.dependsOn deleteGenertedFiles
assembleJreForWindows.dependsOn deleteGenertedFiles
assembleJreForMac.dependsOn deleteGenertedFiles

task showGenerated() {
    print("Showing Logs...")
    def stdout = new ByteArrayOutputStream()
    exec {
        workingDir "$buildDir/distributions/"
        commandLine 'ls'
        standardOutput = stdout
    }
    println "Output:\n$stdout";
}

showGenerated.mustRunAfter packageJreForLinux
